;;; This file is part of Oaklisp.
;;;
;;; This program is free software; you can redistribute it and/or modify
;;; it under the terms of the GNU General Public License as published by
;;; the Free Software Foundation; either version 2 of the License, or
;;; (at your option) any later version.
;;;
;;; This program is distributed in the hope that it will be useful,
;;; but WITHOUT ANY WARRANTY; without even the implied warranty of
;;; MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
;;; GNU General Public License for more details.
;;;
;;; The GNU GPL is available at http://www.gnu.org/licenses/gpl.html
;;; or from the Free Software Foundation, 59 Temple Place - Suite 330,
;;; Boston, MA 02111-1307, USA


;;; Copyright (C) 1987 Kevin J. Lang & Barak A. Pearlmutter

;;; This file contains code that implements fluid variables.

(define top-level-fluid-binding-list (list (cons nil nil)))


(define get-current-fluid-bindings
  (add-method ((make operation))
    (let ((z (or %no-threading (%load-process))))
      (if (eq? z 0)
	  fluid-binding-list
	  (fluid-bindings z)))))

(define set-current-fluid-bindings
  (add-method ((make operation) new-binding-list)
    (let ((z (or %no-threading (%load-process))))
      (if (eq? z 0)
	  (set! fluid-binding-list new-binding-list)
	  (set! (fluid-bindings z) new-binding-list)))))

(define add-to-current-fluid-bindings
  (add-method ((make operation) c-cell)
    (let ((z (or %no-threading (%load-process))))
      (if (eq? z 0)
	  (set! (cdr top-level-fluid-binding-list)
		(cons c-cell (cdr top-level-fluid-binding-list)))
	  (append! (fluid-bindings z) (cons c-cell nil))))))



;;; This is to be called at warm boot time:

(define (revert-fluid-binding-list)
  (set! fluid-binding-list top-level-fluid-binding-list))
;(define (revert-fluid-binding-list)
;  (set-current-fluid-bindings top-level-fluid-binding-list))
;(define (revert-fluid-binding-list)
;  (set-current-fluid-bindings (cons (cons nil nil) nil)))

;;; And at cold boot time too, I suppose:

(revert-fluid-binding-list)

#|
;;; This must be delayed until later in the world building process.
(define-syntax (fluid x)
  `(%fluid ',x))
|#

(define-constant-instance %fluid locatable-operation)

(add-method (%fluid (symbol) sym)
  (iterate aux ()
    (let ((x (%assq sym (get-current-fluid-bindings))))
      (cond (x => cdr)
	    (else (cerror
		   (format #f "Try looking up (FLUID ~S) again." sym)
		   "(FLUID ~S) not found." sym)
		  (aux))))))

(add-method ((setter %fluid) (symbol) sym val)
  (let ((x (%assq sym (get-current-fluid-bindings))))
    (cond (x (set! (cdr x) val))
	  (else (add-to-current-fluid-bindings (cons sym val))
		val))))

(add-method ((locater %fluid) (symbol) sym)
  (iterate aux ()
    (let ((x (%assq sym (get-current-fluid-bindings))))
      (cond (x (make-locative (cdr x)))
	    (else
	     (cerror
	      (format #f "Try looking up (FLUID ~S) again." sym)
	      "Locative to (FLUID ~S) not found." sym)
	     (aux))))))

;;; eof
